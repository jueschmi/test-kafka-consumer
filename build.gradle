buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
        maven { url "http://packages.confluent.io/maven/" }
        maven {
            url "https://nexus.dev.sda-se.com/repository/sda-se-releases/"
            credentials {
                username sdaNexusUser
                password sdaNexusPassword
            }
        }
    }
    dependencies {
        classpath group: 'com.sdase.gradle', name: 'eclipse-settings' , version: '0.+'
        classpath group: 'com.commercehub.gradle.plugin', name: 'gradle-avro-plugin', version: '0.15.+'
        classpath group: 'gradle.plugin.com.github.imflog', name: 'kafka-schema-registry-gradle-plugin', version:'0.4.0'
    }
}

plugins {
    id "org.owasp.dependencycheck" version "3.0.2"
    id "org.sonarqube" version "2.6.1"
    id "pl.allegro.tech.build.axion-release" version "1.9.3"
    id "nebula.dependency-lock" version "4.9.5"
    id "nebula.integtest" version "3.4.0"
    id "application"
    id "maven"
    id "jacoco"
}

apply plugin: "com.sdase.gradle.eclipse-settings"
apply plugin: "com.commercehub.gradle.plugin.avro"
apply plugin: "com.github.imflog.kafka-schema-registry-gradle-plugin"

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://packages.confluent.io/maven/" }
    maven {
        url "https://nexus.dev.sda-se.com/repository/sda-se-releases/"
        credentials {
            username sdaNexusUser
            password sdaNexusPassword
        }
    }
}

group 'com.sdase.service'

sourceCompatibility = 1.8
mainClassName = "com.sda.workbench.kafka.consumer.TestKafkaConsumerApplication"

sonarqube {
    properties {
        property "sonar.projectName", "document-ods-Service"
        property "sonar.exclusions", "**/*.properties"
        property "sonar.jacoco.itReportPath", "build/jacoco/integrationTest.exec"
        property "sonar.jacoco.reportPath", "build/jacoco/test.exec"
        // Workaround since the base url should be picked up from the Jenkins
        property 'sonar.host.url', 'https://sonarqube.dev.sda-se.com'
    }
}

schemaRegistry {
    url = 'http://localhost:8081'
    register {
        subject('test-event-topic-value', 'src/main/avro/DocumentODSEvent.avsc',
                ['src/main/avro/DocumentODSEventType.avsc', 'src/main/avro/SoRKey.avsc'])
    }

    download {
        subject('test-event-topic-value', 'build/schema/downloaded/avro')
    }
}

// release
scmVersion {
    versionIncrementer 'incrementMinor'
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
    }
    tag { prefix = 'v' }
}
version = scmVersion.version

jar {
    version = null
    duplicatesStrategy = 'exclude'
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}

task generateAvro(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask, dependsOn: 'downloadSchemasTask') {
    source("build/schema/downloaded/avro")
    outputDir = file("build/schema/generated/avro")
}

compileJava.source(generateAvro.outputs)


project.ext {
    dropwizardVersion = '1.3.2'
    jerseyVersion = '2.25.1'
    jacksonVersion = '2.9.2'
    kafkaVersion = '0.11.0.1'
    configPath = "$rootProject.projectDir/"
}

dependencies {
    compile group: 'com.sdase.framework', name: 'kafka-common', version: '0.14.0'
    compile group: 'com.sdase.framework', name: 'mapper-common', version: '0.6.0'
    compile group: 'com.sdase.framework', name: 'rest-common', version: '0.47.0'
    compile group: 'com.sdase.framework', name: 'dropwizard-weld-bundle', version: '0.11.0'
    compile group: 'com.sdase.mongo', name: 'mongo-common', version: '0.6.0'

    compile group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.280'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    compile group: 'com.lordofthejars', name: 'nosqlunit-mongodb', version: '0.13.1'
    compile group: 'com.meltmedia.dropwizard', name: 'dropwizard-mongo', version: '0.5.0'

    compile group: 'io.dropwizard', name: 'dropwizard-client', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-hibernate', version: dropwizardVersion
    compile group: 'io.dropwizard', name: 'dropwizard-auth', version: dropwizardVersion
    compile group: 'javax', name: 'javaee-api', version: '7.0'
    compile group: 'ma.glasnost.orika', name: 'orika-core', version: '1.5.2'
    compile group: 'org.apache.avro', name: 'avro', version: '1.8.2'
    compile group: 'org.glassfish.jersey.ext.cdi', name: 'jersey-cdi1x', version: jerseyVersion
    compile group: 'org.javamoney', name: 'moneta', version: '1.1'
    compile group: 'org.jboss.weld.se', name: 'weld-se', version: '2.4.3.Final'
    compile group: 'org.jboss.weld.servlet', name: 'weld-servlet-core', version: '2.4.1.Final'
    compile group: 'org.mongodb.morphia', name: 'morphia', version: '1.3.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'org.zapodot', name: 'hystrix-dropwizard-bundle', version: '0.9.2'
    compile('io.swagger:swagger-jaxrs:1.5.16')

    compile('io.confluent:kafka-avro-serializer:3.2.1') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    runtime group: 'com.github.database-rider', name: 'rider-parent', version: '1.2.3'
}


run { args 'server', configPath + 'config.eclipse.yml' }
